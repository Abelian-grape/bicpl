dnl Process this file with autoconf to produce a configure script.

AC_INIT(Include/bicpl.h)
AM_INIT_AUTOMAKE(bicpl, 1.3.1)
AM_CONFIG_HEADER(config.h)

dnl Add extra include or link directories
AC_ARG_WITH(build-path,[
  --with-build-path[=DIR] use DIR/include DIR/lib
],[
for d in `echo $withval | tr : ' '`; do
    test -d $d/include && CPPFLAGS="$CPPFLAGS -I$d/include"
    test -d $d/lib && LDFLAGS="$LDFLAGS -L$d/lib"
done
])


AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL


smr_CHECK_LIB(netcdf,,
[NetCDF library], ncopen, netcdf.h)
if test "$netcdf_LIBS" = ""; then
    AC_MSG_ERROR([Required library NetCDF not found!])
fi

smr_CHECK_LIB(minc,,
[MNI medical image library], miicv_create, minc.h, 
$netcdf_LIBS, $netcdf_CFLAGS)
if test "$minc_LIBS" = ""; then
    AC_MSG_ERROR([Required library minc not found!])
fi

smr_CHECK_LIB(volumeio,volume_io,
[MNI Volume IO library], input_volume, volume_io.h,
[$netcdf_LIBS $minc_LIBS -lm],[$netcdf_CFLAGS $minc_CFLAGS])
if test "$volumeio_LIBS" = ""; then
    AC_MSG_ERROR([Required library volume_io not found!])
fi

CFLAGS="$volumeio_CFLAGS $minc_CFLAGS $netcdf_CFLAGS"
LIBS="$volumeio_LIBS $minc_LIBS $netcdf_LIBS"


dnl Decide which file format is used for images.
dnl Prefer PPM if available, otherwise use SGI's irisimage,
dnl otherwise no image I/O is possible

imageio_GLUE_CODE=rgb_io_none.c

smr_CHECK_LIB(ppm,netpbm,
[Portable PixMap library], ppm_writeppminit, ppm.h)
if test "$ppm_LIBS" != ""; then
    imageio_CFLAGS="$ppm_CFLAGS"
    imageio_LIBS="$ppm_LIBS"
    imageio_GLUE_CODE=rgb_io_ppm.c
else
    smr_CHECK_LIB(irisimage, image,
    [SGI image library], iopen, image.h)
    if test "$irisimage_LIBS" != ""; then
	imageio_CFLAGS="$irisimage_CFLAGS"
	imageio_LIBS="$irisimage_LIBS"
	imageio_GLUE_CODE=rgb_io_sgi.c
    fi
fi

AC_SUBST(imageio_CFLAGS)
AC_SUBST(imageio_LIBS)

AC_DEFINE_UNQUOTED(imageio_GLUE_CODE,
    ["$imageio_GLUE_CODE"],[File to include in Images/rgb_io.c])




dnl AC_CHECK_TYPE(bool,int)

AC_OUTPUT( 
Makefile
Include/Makefile Include/bicpl/Makefile
Data_structures/Makefile
Transforms/Makefile
Geometry/Makefile
Numerical/Makefile
Objects/Makefile
Prog_utils/Makefile
Volumes/Makefile
Marching_cubes/Makefile
Images/Makefile
)
